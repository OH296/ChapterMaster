function distribute_strength_to_fleet(strength, fleet) {
    while (strength > 0) {
        var ship_type = choose(1, 1, 1, 1, 2, 2, 3);
        strength -= ship_type;
        if (ship_type == 1) {
            fleet.escort_number++;
        } else if (ship_type == 2) {
            fleet.frigate_number++;
        } else if (ship_type == 3) {
            fleet.capital_number++;
        }
    }
}

/// @mixin obj_en_fleet
function random_sector_exit_point() {
    action_x = choose(room_width * -1, room_width * 2);
    action_y = choose(room_height * -1, room_height * 2);
}

/// @mixin obj_en_fleet
function in_room(object = undefined) {
    if (object == undefined) {
        object = self;
    }
    return !(object.x < 0 || object.x > room_width || object.y < 0 || object.y > room_height);
}

//to be run within with scope
function set_fleet_target(targ_x, targ_y, final_target) {
    action_x = targ_x;
    action_y = targ_y;
    target = final_target;
    action_eta = floor(point_distance(x, y, targ_x, targ_y) / 128) + 1;
}

function scr_valid_fleet_target(target) {
    if (target == noone) {
        return false;
    }
    if (is_string(target)) {
        target = noone;
        return false;
    }
    var valid = instance_exists(target);
    if (valid) {
        valid = target.object_index == obj_p_fleet || target.object_index == obj_en_fleet;
    }
    return valid;
}

function get_fleet_uid(search_uid){
    var _fleet = undefined;
    with (obj_en_fleet){
        if (uid == search_uid){
            _fleet = id;
            break;
        }
    }
    return _fleet;
}

function fleets_next_location(fleet = "none", visited = []) {
    var targ_location = "none";

    if (fleet == "none") {
        fleet = self;
    }

    if (instance_exists(fleet)) {
        // Add the current fleet's ID to the visited list to avoid rechecking it
        array_push(visited, fleet.id);

        // Check if the fleet has a 'target' variable
        if (variable_instance_exists(fleet, "target")) {
            // If the target is valid and not already in the visited list, proceed recursively
            var fleet_target_valid = scr_valid_fleet_target(fleet.target);
            if (!fleet_target_valid) {
                fleet.target = 0;
            }
            if (fleet_target_valid && !array_contains(visited, fleet.target.id)) {
                // Recursive call with the target and the updated visited list
                targ_location = fleets_next_location(fleet.target, visited);
            } else if (fleet.action != "") {
                // If no valid target, use the fleet's action coordinates
                targ_location = instance_nearest(fleet.action_x, fleet.action_y, obj_star);
            } else {
                // Default to nearest star to fleet's current position
                targ_location = instance_nearest(fleet.x, fleet.y, obj_star);
            }
        }
    }
    // If targ_location was not set to anything else, default to the nearest star
    if (targ_location == "none") {
        targ_location = instance_nearest(fleet.x, fleet.y, obj_star);
    }
    return targ_location;
}

function chase_fleet_target_set(target) {
    var targ_location = fleets_next_location(target);
    if (targ_location != "none") {
        action_x = targ_location.x;
        action_y = targ_location.y;
        action = "";
        set_fleet_movement();
    }
}

/// @mixin
function fleet_intercept_time_calculate(target_intercept) {
    var intercept_time = -1;
    var targ_location = fleets_next_location(target_intercept);
    if (instance_exists(targ_location)) {
        intercept_time = floor(point_distance(targ_location.x, targ_location.y, action_x, action_y) / action_spd) + 1;
    }
    return intercept_time;
}

function get_largest_player_fleet() {
    var chosen_fleet = "none";
    if (instance_exists(obj_p_fleet)) {
        with (obj_p_fleet) {
            if (point_in_rectangle(x, y, 0, 0, room_width, room_height) && point_in_rectangle(action_x, action_y, 0, 0, room_width, room_height)) {
                if (chosen_fleet == "none") {
                    chosen_fleet = self;
                    continue;
                }
                if (!(capital_number == 0 && chosen_fleet.capital_number == 0)) {
                    if (capital_number > chosen_fleet.capital_number) {
                        chosen_fleet = self;
                    }
                } else if (!(frigate_number == 0 && chosen_fleet.frigate_number == 0)) {
                    if (frigate_number > chosen_fleet.frigate_number) {
                        chosen_fleet = self;
                    }
                } else if (!(escort_number == 0 && chosen_fleet.escort_number == 0)) {
                    if (escort_number > chosen_fleet.escort_number) {
                        chosen_fleet = self;
                    }
                }
            }
        }
    }
    return chosen_fleet;
}

function is_orbiting(fleet = "none") {
    if (fleet == "none") {
        if (action != "") {
            return false;
        }
        try {
            var nearest = instance_nearest(x, y, obj_star);
            if (point_distance(x, y, nearest.x, nearest.y) < 10 && nearest.name != "") {
                orbiting = nearest.id;
                return true;
            }
            orbiting = false;
        } catch (_exception) {
            return false;
        }
        return false;
    } else {
        with (fleet) {
            return is_orbiting();
        }
    }
}

/// @mixin
function set_fleet_movement(fastest_route = true, new_action = "move", minimum_eta = 1, maximum_eta = 1000) {
    action = "";

    if (action == "") {
        turns_static = 0;
        var mine, fleet;
        var connected = 0, cont = 0, target_dist = 0;
        if (fastest_route) {
            mine = instance_nearest(x, y, obj_star);
            var star_travel = new FastestRouteAlgorithm(x, y, action_x, action_y, self.id, is_orbiting());
            var path = star_travel.final_array_path();
            if (array_length(path) > 1) {
                var targ = find_star_by_name(path[1]);
                if (targ != "none") {
                    array_delete(path, 0, 2);
                    complex_route = path;
                    action_x = targ.x;
                    action_y = targ.y;
                    set_fleet_movement(false, new_action);
                } else {
                    set_fleet_movement(false, new_action);
                }
            } else {
                set_fleet_movement(false, new_action);
            }
        } else {
            var _target_sys = instance_nearest(action_x, action_y, obj_star);
            var _target_is_sys = false;

            if (instance_exists(_target_sys)) {
                _target_is_sys = point_distance(_target_sys.x, _target_sys.y, action_x, action_y) < 10;
            }

            mine = instance_nearest(x, y, obj_star);

            var eta = calculate_fleet_eta(x, y, action_x, action_y, action_spd, _target_is_sys, is_orbiting(), warp_able);
            action_eta = eta;
            if ((action_eta <= 0) || (owner != eFACTION.INQUISITION)) {
                action_eta = eta;
            } else if ((owner == eFACTION.INQUISITION) && (action_eta < 2) && (string_count("_her", trade_goods) == 0)) {
                action_eta = 2;
            }
            if (is_orbiting()) {
                if (owner != eFACTION.ELDAR && mine.storm) {
                    action_eta += 10000;
                }
            }

            // action_x=sys.x;
            // action_y=sys.y;
            orbiting = false;
            action = new_action;
            action_eta = clamp(action_eta, minimum_eta, maximum_eta);
        }
    }
}

//TODO build into unit struct
function load_unit_to_fleet(fleet, unit) {
    var loaded = false;
    var all_ships = fleet_full_ship_array(fleet);

    for (var i = 0; i < array_length(all_ships); i++) {
        var ship_ident = all_ships[i];
        if (obj_ini.ship_capacity[ship_ident] > obj_ini.ship_carrying[ship_ident]) {
            obj_ini.ship_carrying[ship_ident] += unit.size;
            unit.planet_location = 0;
            unit.location_string = obj_ini.ship_location[ship_ident];
            unit.ship_location = ship_ident;
            loaded = true;
            break;
        }
    }
    return loaded;
}

function calculate_fleet_eta(xx, yy, xxx, yyy, fleet_speed, star1 = true, star2 = true, warp_able = false) {
    var warp_lane = false;
    var eta = 0;
    //Some duke unfinished webway stuff copied here for reference
    /*for (var w = 1;w<5;w++){
			if (planet_feature_bool(mine.p_feature[w], eP_FEATURES.WEBWAY)==1) then web1=1;
			if (planet_feature_bool(sys.p_feature[w], eP_FEATURES.WEBWAY)==1) then web2=1;
		}*/
    if (star1 && star2) {
        star1 = instance_nearest(xx, yy, obj_star);
        star2 = instance_nearest(xxx, yyy, obj_star);
        warp_lane = determine_warp_join(star1.id, star2.id);
    } else if (star1) {
        star1 = instance_nearest(xx, yy, obj_star);
    }
    eta = floor(point_distance(xx, yy, xxx, yyy) / fleet_speed) + 1;
    if (!warp_lane) {
        eta *= 2;
    }
    if (warp_lane && warp_able) {
        eta = ceil(eta / warp_lane);
    }
    if (!star2) {
        return eta;
    }

    //check end location for warp storm
    if (instance_exists(star2)) {
        if (star2.object_index == obj_star) {
            if (star2.storm) {
                eta += 10000;
            }
        }
    }
    return eta;
}

/// @mixin
function calculate_action_speed(fleet = "none", selected = false) {
    try {
        if (fleet == "none") {
            var capitals = 0, frigates = 0, escorts = 0, i;
            var _is_player_fleet = object_index == obj_p_fleet;
            if (_is_player_fleet) {
                if (!selected) {
                    player_fleet_ship_count();
                    capitals = capital_number;
                    frigates = frigate_number;
                    escorts = escort_number;
                } else {
                    //TODO extract to a fleet selected function
                    var types = selected_ship_types();
                    capitals = types[0];
                    frigates = types[1];
                    escorts = types[2];
                }
            }
            var fleet_speed = 128;
            if (capitals > 0) {
                fleet_speed = 100;
            } else if (frigates > 0) {
                fleet_speed = 128;
            } else if (escorts > 0) {
                fleet_speed = 174;
            }
            if (_is_player_fleet) {
                if ((obj_controller.stc_ships >= 6) && (fleet_speed >= 100)) {
                    fleet_speed *= 1.2;
                }
            }
            return fleet_speed;
        } else {
            with (fleet) {
                return calculate_action_speed(, selected);
            }
        }
    } catch (_exception) {
        handle_exception(_exception);
        return 200;
    }
}

/// @mixin
function scr_efleet_arrive_at_trade_loc() {
    //if player fleet at star or player forces trade
    var chase_fleet = false;

    var _valid_fleet = false;
    var _orbit = orbiting;
    var _valid_planet = false;

    var _viewer = obj_controller.location_viewer;
    if (orbiting.owner < 6 && _viewer.has_troops(orbiting.name)) {
        _valid_planet = true;
    }

    with (obj_p_fleet) {
        if (x == _orbit.x && y == _orbit.y) {
            _valid_fleet = true;
            break;
        }
    }

    //iff no forces see iffleet to chase
    if (!_valid_fleet && !_valid_planet) {
        var _chase_target = -1;
        if (instance_exists(target) && target.object_index == obj_p_fleet) {
            _chase_target = target;
        } else {
            target = instance_nearest(x, y, obj_p_fleet);
        }
        var _chase_fleet = instance_exists(target) && (target.action != "" || point_distance(x, y, target.x, target.y) > 40) && obj_ini.fleet_type != ePLAYER_BASE.HOME_WORLD;

        if (_chase_fleet) {
            if (!string_count("Inqis", trade_goods)) {
                if (target.action != "") {
                    action_x = target.action_x;
                    action_y = target.action_y;
                } else if (target.action == "") {
                    var _targ_star = instance_nearest(target.x, target.y, obj_star);
                    action_x = _targ_star.x;
                    action_y = _targ_star.y;
                }
                action = "";
                set_fleet_movement();
                if (owner != eFACTION.ELDAR) {
                    obj_controller.disposition[owner] -= 1;
                }
            }
        }

        //if no fleet find a valid planet with player forces
        if (action == "") {
            var _player_star = nearest_star_with_ownership(x, y, 1);
            if (_player_star != "none") {
                action_x = _player_star.x;
                action_y = _player_star.y;
                set_fleet_movement();
            } else {
                var _player_presence_stars = _viewer.player_force_stars();
                if (array_length(_player_presence_stars)) {
                    var _nearest_index = nearest_from_array(x, y, _player_presence_stars);
                    var _nearest = _player_presence_stars[_nearest_index];
                    action_x = _nearest.x;
                    action_y = _nearest.y;
                    set_fleet_movement();
                }
            }
        }

        //if no other viable options drop off at random imperial planet
        if (action == "") {
            var _imp = nearest_star_with_ownership(x, y, 2);
            if (_imp != "none") {
                if (x == _imp.x && y == _imp.y) {
                    _valid_planet = true;
                } else {
                    action_x = _imp.x;
                    action_y = _imp.y;
                    set_fleet_movement();
                }
            }
        }
    }

    if (_valid_fleet || _valid_planet) {
        var targ;
        var cur_star = nearest_star_proper(x, y);
        var bleh = "";
        if (owner != eFACTION.INQUISITION) {
            bleh = $"{obj_controller.faction[owner]} Fleet finalizes trade at {cur_star.name}.";
        } else {
            bleh = $"Inquisitor Ship finalizes trade at {cur_star.name}.";
        }
        LOGGER.info(bleh);
        scr_alert("green", "trade", bleh, cur_star.x, cur_star.y);
        scr_event_log("", bleh, cur_star.name);

        // Drop off here
        if (fleet_has_cargo("player_goods")) {
            scr_trade_dep();
        }

        if (target != noone) {
            target = noone;
        }

        if (owner == eFACTION.ELDAR) {
            cur_star = nearest_star_with_ownership(xx, yy, eFACTION.ELDAR);
            if (cur_star != "none") {
                cur_star = targ.x;
                cur_star = targ.y;
            }
        } else {
            action_x = home_x;
            action_y = home_y;
            set_fleet_movement();
        }
        trade_goods = "return";
        if (action_eta == 0) {
            instance_destroy();
        }
        return true;
    }
    return false;
}

/// @function scr_orbiting_fleet(faction, system)
/// @description Returns the ID of a fleet orbiting the given system/star that matches the specified faction.
/// @param {any|array} faction
/// The faction identifier to check against. Can be a single faction ID or an array of multiple factions.
/// @param {any} [system="none"]
/// The system instance or star to check. If `"none"`, the function uses the calling instance's position.
/// @returns {real|string} The ID of the matching fleet instance, or `"none"` if no valid fleet is found.
///
/// @example
/// ```gml
/// // Find a fleet orbiting this star that belongs to faction 3
/// var fleet_id = scr_orbiting_fleet(3);
/// if (fleet_id != "none") {
///     LOGGER.debug("Faction fleet found: " + string(fleet_id));
/// }
///
/// // Find fleets from multiple factions
/// var factions = [1, 2, 5];
/// var fleet_id = scr_orbiting_fleet(factions, some_system);
/// ```
///

function scr_orbiting_fleet(faction, system = "none") {
    var _found_fleet = "none";
    var _faction_list = is_array(faction);
    var xx = system == "none" ? x : system.x;
    var yy = system == "none" ? y : system.y;
    with (obj_en_fleet) {
        if (x == xx && y == yy) {
            var _valid = false;
            if (_faction_list) {
                _valid = array_contains(faction, owner);
            } else {
                if (owner == faction) {
                    _valid = true;
                }
            }
            if (_valid && action == "") {
                _found_fleet = id;
                break;
            }
        }
    }
    return _found_fleet;
}

/// @function object_distance(obj_1, obj_2)
/// @description Returns the distance in pixels between two instances or objects based on their `x` and `y` coordinates.
/// @param {instance} obj_1 The first object or instance.
/// @param {instance} obj_2 The second object or instance.
/// @returns {real} The distance in pixels between `obj_1` and `obj_2`.
///
/// @example
/// ```gml
/// var dist = object_distance(player, enemy);
/// if (dist < 100) {
///     LOGGER.debug("Enemy is within range!");
/// }
/// ```
///

function object_distance(obj_1, obj_2) {
    return point_distance(obj_1.x, obj_1.y, obj_2.x, obj_2.y);
}

/// @function scr_orbiting_player_fleet(system)
/// @description Returns the ID of the nearest player fleet orbiting the given system or star.
/// @param {any} [system="none"]
/// The system instance or identifier to check. If `"none"`, the function checks the calling star instance.
/// @returns {real} The instance ID of the orbiting player fleet, or -1 if none is found.
///
/// @example
/// ```gml
/// var fleet_id = scr_orbiting_player_fleet();
/// if (fleet_id != -1) {
///     LOGGER.debug("Fleet orbiting star: " + string(fleet_id));
/// }
/// ```
///
function scr_orbiting_player_fleet(system = "none") {
    if (system == "none" && !is_struct(self) && object_index == obj_star) {
        var _fleet = instance_nearest(x, y, obj_p_fleet);
        if (object_distance(self, _fleet) > 0) {
            return -1;
        } else {
            return _fleet.id;
        }
    } else if (system != "none") {
        try {
            with (system) {
                return scr_orbiting_player_fleet();
            }
        } catch (_exception) {
            handle_exception(_exception);
        }
    }

    return -1;
}

function get_orbiting_fleets(faction, system = "none") {
    var _fleets = [];
    var _faction_list = is_array(faction);
    var xx = system == "none" ? x : system.x;
    var yy = system == "none" ? y : system.y;
    with (obj_en_fleet) {
        if (x == xx && y == yy) {
            var _valid = false;
            if (_faction_list) {
                _valid = array_contains(faction, owner);
            } else {
                if (owner == faction) {
                    _valid = true;
                }
            }
            if (_valid && action == "") {
                array_push(_fleets, id);
            }
        }
    }
    return _fleets;
}

function sector_imperial_fleet_strength() {
    obj_controller.imp_ships = 0;
    var _imperial_planet_count = 0;
    var _mech_worlds = 0;
    with (obj_en_fleet) {
        if (owner == eFACTION.IMPERIUM) {
            var _imperial_fleet_defence_score = capital_number + (frigate_number / 2) + (escort_number / 4);
            obj_controller.imp_ships += _imperial_fleet_defence_score;
        }
    }
    with (obj_star) {
        for (var i = 0; i <= planets; i++) {
            var _owner_imperial = p_owner[i] < 5 && p_owner[i] > 1;
            _imperial_planet_count += _owner_imperial;
        }
        if (owner == eFACTION.MECHANICUS) {
            _mech_worlds++;
        }
    }
    max_fleet_strength = (_imperial_planet_count / 8) * (_mech_worlds * 3);
}

function fleet_star_draw_offsets() {
    var coords = [
        0,
        0
    ];
    switch (owner) {
        case eFACTION.IMPERIUM:
            if (!navy) {
                coords = [
                    0,
                    -24
                ]; //
            } else {
                coords = [
                    0,
                    24
                ];
            }
            break;
        case eFACTION.MECHANICUS:
            coords = [
                0,
                -32
            ]; //
            break;
        case eFACTION.INQUISITION:
            coords = [
                0,
                -32
            ]; //
            break;
        case eFACTION.ELDAR:
            coords = [
                -24,
                -24
            ]; //
            break;
        case eFACTION.ORK:
            coords = [
                30,
                0
            ]; //
            break;
        case eFACTION.TAU:
            coords = [
                -24,
                -24
            ]; //
            break;
        case eFACTION.TYRANIDS:
            coords = [
                0,
                32
            ]; //
            break;
        case eFACTION.CHAOS:
            coords = [
                -30,
                0
            ]; //
            break;
        case eFACTION.NECRONS:
            coords = [
                32,
                32
            ]; //
            break;
    }
    return coords;
}

//TODO further split this shite up
/// @mixin
function fleet_arrival_logic() {
    var cur_star, sta, steh_dist, old_x, old_y;
    cur_star = instance_nearest(action_x, action_y, obj_star);
    x = cur_star.x;
    y = cur_star.y;
    sta = instance_nearest(action_x, action_y, obj_star);
    is_orbiting();

    // cur_star.present_fleets+=1;if (owner = eFACTION.TAU) then cur_star.tau_fleets+=1;

    if (owner == eFACTION.MECHANICUS) {
        if (trade_goods == "mars_spelunk1") {
            trade_goods = "mars_spelunk2";
            action_x = home_x;
            action_y = home_y;
            action_eta = 52;
            action = "move";
            exit;
        } else if (trade_goods == "mars_spelunk2") {
            // Unload techmarines nao plz
            scr_mission_reward("mars_spelunk", instance_nearest(home_x, home_y, obj_star), 1);
            instance_destroy();
        }
    }

    //TODO create oppertunity to purge new colonisers if they have taint and the player has garrisons or control of the planet
    if (fleet_has_cargo("colonize")) {
        deploy_colonisers(cur_star);
    }

    if (trade_goods == "return") {
        // with(instance_nearest(x,y,obj_star)){present_fleets-=1;}
        instance_destroy();
    }

    if (owner == eFACTION.INQUISITION) {
        if (fleet_has_cargo("radical_inquisitor")) {
            radical_inquisitor_mission_ship_arrival();
            exit;
        }
    }

    if (!navy) {
        if (trade_goods == "merge") {
            if (is_orbiting()) {
                var _orbit = orbiting;
                var _viable_merge = false;
                var _merge_fleet = false;
                var _imperial_fleets = get_orbiting_fleets(eFACTION.IMPERIUM, _orbit);
                for (var i = 0; i < array_length(_imperial_fleets); i++) {
                    var _fleet = _imperial_fleets[i];
                    if (!_fleet.navy && _fleet.id != id) {
                        _viable_merge = true;
                        _merge_fleet = _fleet;
                        break;
                    }
                }
                if (_viable_merge) {
                    merge_fleets(_merge_fleet.id, id);
                    exit;
                } else {
                    trade_goods = "";
                }
            }
        }

        var cancel = false;
        if (string_count("Inqis", trade_goods) > 0) {
            cancel = true;
        }
        if (string_count("merge", trade_goods) > 0) {
            cancel = true;
        }
        if (trade_goods == "cancel_inspection") {
            cancel = true;
        }
        if (trade_goods == "|DELETE|") {
            cancel = true;
        }
        if (trade_goods == "return") {
            cancel = true;
        }
        if (string_count("_her", trade_goods) > 0) {
            cancel = true;
        }
        if (string_count("investigate_dead", trade_goods) > 0) {
            cancel = true;
        }
        if (string_count("spelunk", trade_goods) > 0) {
            cancel = true;
        }
        if (fleet_has_cargo("warband")) {
            cancel = true;
        }
        if (fleet_has_cargo("ork_warboss")) {
            cancel = true;
        }
        if (fleet_has_cargo("csm")) {
            cancel = true;
        }

        if (!cancel && ((trade_goods != "return" && owner != eFACTION.TYRANIDS && owner != eFACTION.CHAOS) && fleet_has_cargo("player_goods"))) {
            if (scr_efleet_arrive_at_trade_loc()) {
                exit;
            }
        }
    }

    if ((owner == eFACTION.INQUISITION) && (string_count("_her", trade_goods) == 0)) {
        if ((cur_star.owner == eFACTION.PLAYER) && (trade_goods == "cancel_inspection")) {
            instance_deactivate_object(cur_star);
            repeat (choose(1, 2)) {
                orbiting = instance_nearest(x, y, obj_star);
                instance_deactivate_object(orbiting);
            }

            repeat (5) {
                orbiting = instance_nearest(x, y, obj_star);
                if (orbiting.owner == eFACTION.ELDAR) {
                    instance_deactivate_object(orbiting);
                }
            }

            orbiting = instance_nearest(x, y, obj_star);
            action_x = orbiting.x;
            action_y = orbiting.y;
            set_fleet_movement();
            instance_activate_object(obj_star);
            trade_goods += "|DELETE|";
            exit;
        }
    }

    /*if (owner = eFACTION.IMPERIUM) and (guardsmen>0){// 135 ; guardsmen onto planet
        var en_p,en_planets,land,i;
        i=0;en_planets=0;land=0;
        
        if (sta.x=home_x) and (sta.y=home_y){
            repeat(4){i+=1;
                en_p[i]=0;
                if (sta.p_owner[i]<=5){en_p[i]=1;en_planets+=1;}
            }
            
            if (guardsmen>0) and (en_planets>0){
                land=floor(guardsmen/en_planets);
                i=0;
                repeat(4){i+=1;
                    if (en_p[i]=1){guardsmen-=land;sta.p_guardsmen[i]+=land;}
                }
                if (guardsmen<5) then guardsmen=0;
            }
        }
        if (sta.owner>5) or ((sta.owner  = eFACTION.PLAYER) and (obj_controller.faction_status[eFACTION.IMPERIUM]="War")){
            repeat(4){i+=1;
                en_p[i]=0;
                if (sta.p_player[i]>0) and (obj_controller.faction_status[eFACTION.IMPERIUM]="War"){en_p[i]=1;en_planets+=1;}
            }
            
            if (guardsmen>0) and (en_planets>0){
                land=floor(guardsmen/en_planets);
                i=0;
                repeat(4){i+=1;
                    if (en_p[i]=1){guardsmen-=land;sta.p_guardsmen[i]+=land;}
                }
                if (guardsmen<5) then guardsmen=0;
            }
        }
    }*/

    if (owner == eFACTION.INQUISITION) {
        if (string_count("DELETE", trade_goods) > 0) {
            instance_destroy();
        }
        if (obj_controller.known[eFACTION.INQUISITION] == 0) {
            obj_controller.known[eFACTION.INQUISITION] = 1;
        }
    } else if (owner == eFACTION.TAU) {
        if (instance_exists(obj_p_ship)) {
            var p_ship = instance_nearest(x, y, obj_p_ship);
            if ((p_ship.action == "") && (point_distance(x, y, p_ship.x, p_ship.y) < 80)) {
                if (obj_controller.p_known[8] == 0) {
                    obj_controller.p_known[8] = 1;
                }
            }
        }
    } else if (owner == eFACTION.TYRANIDS) {
        var mess = 1, plap = instance_nearest(action_x, action_y, obj_p_fleet);

        if (instance_exists(plap)) {
            if (point_distance(plap.x, plap.y, action_x, action_y) < 80) {
                mess = 0;
            }
        }

        if ((mess == 1) && (sta.vision != 0)) {
            scr_alert("red", "owner", $"Contact has been lost with {sta.name}!", sta.x, sta.y);
            scr_event_log("red", $"Contact has been lost with {sta.name}.");
            sta.vision = 0;
        }
    }
    action_x = 0;
    action_y = 0;

    // 135 ; fleet chase
    if ((string_count("Inqis", trade_goods) > 0) && (string_count("fleet", trade_goods) > 0) && (!string_count("_her", trade_goods))) {
        inquisition_fleet_inspection_chase();
    }

    old_x = x;
    old_y = y;
    x = -100;
    y = -100;

    cur_star = instance_nearest(old_x, old_y, obj_en_fleet);
    var mergus = false;

    mergus = cur_star.image_index;
    if (mergus < 3) {
        mergus = 0;
    }
    if (mergus >= 3) {
        mergus = 10;
    }
    if ((owner == eFACTION.TAU) && (mergus >= 3)) {
        mergus = 0;
    }
    if (string_count("_her", trade_goods) == 0) {
        mergus = 99;
    } // was 999

    // Think this might be causing the crash
    if ((owner == eFACTION.TAU) && (sta.present_fleet[eFACTION.IMPERIUM] + sta.present_fleet[eFACTION.PLAYER] >= 1) && (sta.present_fleet[eFACTION.TAU] == 1) && (image_index == 1) && (ret == 0)) {
        mergus = 15;
    }
    if ((cur_star.owner == eFACTION.TAU) && (owner == eFACTION.TAU) && (ret == 1)) {
        mergus = 0;
    }

    if ((owner == eFACTION.TAU) && (image_index == 1)) {
        // show_message("Tau|||  Other Owner: "+string(cur_star.owner)+"   ret: "+string(ret)+"    mergus: "+string(mergus));
    }

    if ((owner == eFACTION.CHAOS) && fleet_has_cargo("csm") || fleet_has_cargo("warband")) {
        mergus = 0;
    }
    // if (cur_star.owner!=owner) then mergus=0;

    if ((cur_star.x == old_x) && (cur_star.y == old_y) && (cur_star.owner == self.owner) && (cur_star.action == "") && (mergus == 1999)) {
        // Merge the fleets
        cur_star.escort_number += self.escort_number;
        cur_star.frigate_number += self.frigate_number; // show_message("Tau fleet merging");
        cur_star.capital_number += self.capital_number;
        cur_star.guardsmen += self.guardsmen;

        cur_star = instance_nearest(old_x, old_y, obj_star);
        // if (cur_star.present_fleets>=1) then cur_star.present_fleets-=1;
        if (owner == eFACTION.TAU) {
            obj_controller.tau_fleets -= 1;
            cur_star.tau_fleets -= 1;
        }
        if (owner == eFACTION.CHAOS) {
            obj_controller.chaos_fleets -= 1;
        }

        instance_destroy();
    } // End merge fleets

    if ((owner == eFACTION.TAU) && (mergus == 15)) {
        // Get the fuck out
        var new_star, stue;
        new_star = 0;
        stue = 0;
        ret = 1;

        instance_activate_object(obj_star); // new_star
        stue = instance_nearest(x, y, obj_star);

        if (image_index == 1) {
            // Start influence thing
            var tau_influence;
            var tau_influence_chance = irandom(100) + 1;
            var tau_influence_planet = irandom(stue.planets) + 1;

            with (stue) {
                if (p_type[tau_influence_planet] != "Dead") {
                    scr_alert("green", "owner", $"Tau ship broadcasts subversive messages to {planet_numeral_name(tau_influence_planet)}.", sta.x, sta.y);
                    tau_influence = p_influence[tau_influence_planet][eFACTION.TAU];

                    if ((tau_influence_chance <= 70) && (tau_influence < 70)) {
                        adjust_influence[tau_influence_planet](eFACTION.TAU, 10, tau_influence_planet);
                        if (p_type[tau_influence_planet] == "Forge") {
                            adjust_influence(eFACTION.TAU, -5, tau_influence_planet);
                        }
                    }

                    if ((tau_influence_chance <= 3) && (tau_influence < 70)) {
                        adjust_influence(eFACTION.TAU, 30, tau_influence_planet);
                        if (p_type[tau_influence_planet] == "Forge") {
                            adjust_influence(eFACTION.TAU, -25, tau_influence_planet);
                        }
                    }
                }
            }
        }

        instance_deactivate_object(stue);

        with (obj_star) {
            if (owner != eFACTION.TAU) {
                instance_deactivate_object(instance_id);
            }
        }

        var good;
        good = 0;

        repeat (100) {
            var xx, yy;
            if (good == 0) {
                xx = x + choose(random(300), random(300) * -1);
                yy = y + choose(random(300), random(300) * -1);
                new_star = instance_nearest(xx, yy, obj_star);
                if (new_star.owner != eFACTION.TAU) {
                    with (new_star) {
                        instance_deactivate_object(id);
                    }
                }
                if (new_star.owner == eFACTION.TAU) {
                    good = 1;
                }
            }
        }

        // show_message("Get the fuck out working?: "+string(good));

        if (new_star.owner == eFACTION.TAU) {
            // show_message("Tau fleet actually fleeing");
            action_x = new_star.x;
            action_y = new_star.y;
            set_fleet_movement();
        }

        instance_activate_object(obj_star);
        // This appears bugged
    }

    x = old_x;
    y = old_y;

    var _csm = fleet_has_cargo("warband");

    if ((cur_star.x == old_x) && (cur_star.y == old_y) && (cur_star.owner == self.owner) && (cur_star.action == "") && ((owner == eFACTION.TAU) || (owner == eFACTION.CHAOS)) && (mergus == 10) && (!_csm)) {
        // Move somewhere new
        var stue, stue2;
        stue = 0;
        stue2 = 0;
        var goood = 0;

        with (obj_star) {
            if (is_dead_star()) {
                instance_deactivate_object(id);
            }
        }
        stue = instance_nearest(x, y, obj_star);
        instance_deactivate_object(stue);
        repeat (10) {
            if (goood == 0) {
                stue2 = instance_nearest(x + choose(random(400), random(400) * -1), y + choose(random(400), random(400) * -1), obj_star);
                if ((owner == eFACTION.TAU) && (stue2.owner == eFACTION.TAU)) {
                    goood = 1;
                }
                if ((owner == eFACTION.CHAOS) && (stue2.owner != eFACTION.CHAOS)) {
                    goood = 1;
                }
                if (stue2.planets == 0) {
                    goood = 0;
                }
                if ((stue.present_fleet[eFACTION.IMPERIUM] > 0) || (stue.present_fleet[eFACTION.PLAYER] > 0)) {
                    goood = 0;
                }
                if ((stue2.planets == 1) && (stue2.p_type[1] == "Dead")) {
                    goood = 0;
                }
            }
        }
        action_x = stue2.x;
        action_y = stue2.y;
        set_fleet_movement(); // stue.present_fleets-=1;
        instance_activate_object(obj_star);
    }

    // ORKS
    // Right here check to see if the fleet is being useless
    // If yes check for connected planet, see if not owned by orks
    // If not owned by orks then start heading that way
    // If the connected planet is owned by orks then choose a random one within 400 not owned by orks

    if (owner == eFACTION.ORK) {
        if (is_orbiting()) {
            with (orbiting) {
                ork_fleet_arrive_target();
            }
        }

        var kay = 0, temp5 = 0, temp6 = 0, temp7 = 0;

        var cur_star = instance_nearest(x, y, obj_star);

        // This is the new check to go along code; if doesn't add up to all planets = 7 then they exit
        if (!is_dead_star(cur_star)) {
            // KILL the enemy
            if ((cur_star.present_fleet[1] > 1) || (cur_star.present_fleet[2] > 1)) {
                exit;
            }
        }

        if (((cur_star.owner == eFACTION.CHAOS) && (image_index >= 5) && (owner == eFACTION.CHAOS)) || ((owner == eFACTION.CHAOS) && (image_index >= 5) && (cur_star.planets == 0))) {
            kay = 50;
        }

        if (kay == 50) {
            if (owner == eFACTION.ORK) {
                with (obj_star) {
                    if (owner == eFACTION.ORK) {
                        instance_deactivate_object(instance_id);
                    }
                }
            }

            repeat (20) {
                if (kay == 50) {
                    temp5 = x + choose(random(300), random(300) * -1);
                    temp6 = y + choose(random(300), random(300) * -1);
                    temp7 = instance_nearest(temp5, temp6, obj_star);

                    if ((owner == eFACTION.ORK) && (temp7.owner != eFACTION.ORK) && (temp7.planets > 0) && (temp7.image_alpha >= 1)) {
                        kay = 55;
                    }
                    if ((owner == eFACTION.TAU) && (temp7.owner != eFACTION.TAU) && (temp7.planets > 0) && (temp7.image_alpha >= 1)) {
                        kay = 55;
                    }
                    if ((owner == eFACTION.CHAOS) && (temp7.owner != eFACTION.CHAOS) && (temp7.planets > 0) && (temp7.image_alpha >= 1)) {
                        kay = 55;
                    }
                }
            }

            if ((kay == 55) && instance_exists(temp7)) {
                action_x = temp7.x;
                action_y = temp7.y;
                set_fleet_movement();

                // cur_star.present_fleets-=1;
            }

            instance_activate_object(obj_star);
        } else {
            cur_star.present_fleet[eFACTION.ORK]++;
        }

        instance_activate_object(obj_star);
    }

    exit; // end of eta=0
}

function choose_fleet_sprite_image() {
    if (owner == eFACTION.IMPERIUM && !fleet_has_cargo("colonize")) {
        sprite_index = spr_fleet_imperial;
    } else if (owner == eFACTION.IMPERIUM && fleet_has_cargo("colonize")) {
        sprite_index = spr_fleet_civilian;
    } else if (owner == eFACTION.MECHANICUS) {
        sprite_index = spr_fleet_mechanicus;
    } else if ((owner == eFACTION.INQUISITION) && (string_count("_fleet", trade_goods) > 0) && (target > 0)) {
        target = instance_nearest(target_x, target_y, obj_p_fleet);
    } else if (owner == eFACTION.INQUISITION) {
        sprite_index = spr_fleet_inquisition;
    } else if (owner == eFACTION.ELDAR) {
        sprite_index = spr_fleet_eldar;
    } else if (owner == eFACTION.ORK) {
        sprite_index = spr_fleet_ork;
    } else if (owner == eFACTION.TAU) {
        sprite_index = spr_fleet_tau;
    } else if (owner == eFACTION.TYRANIDS) {
        sprite_index = spr_fleet_tyranid;
    } else if (owner == eFACTION.CHAOS) {
        sprite_index = spr_fleet_chaos;
    }
    image_speed = 0;
}

function merge_fleets(main_fleet, merge_fleet) {
    main_fleet.capital_number += merge_fleet.capital_number;
    main_fleet.frigate_number += merge_fleet.frigate_number;
    main_fleet.escort_number += merge_fleet.escort_number;
    var _merge_cargo = struct_get_names(merge_fleet.cargo_data);
    //TODO custom merge stuff
    for (var i = 0; i < array_length(_merge_cargo); i++) {
        if (!struct_exists(main_fleet.cargo_data, _merge_cargo[i])) {
            main_fleet.cargo_data[$ _merge_cargo[i]] = merge_fleet.cargo_data[$ _merge_cargo[i]];
        }
    }
    instance_destroy(merge_fleet.id);
}

function fleet_respond_crusade() {
    if (owner != eFACTION.IMPERIUM) {
        exit;
    }
    if (!navy) {
        exit;
    }
    if (orbiting.owner > eFACTION.ECCLESIARCHY) {
        exit;
    }
    if (trade_goods != "") {
        exit;
    }
    if (action != "") {
        exit;
    }
    if (guardsmen_unloaded > 0) {
        exit;
    }

    // Crusade AI
    obj_controller.temp[88] = owner;
    with (obj_crusade) {
        if (owner != obj_controller.temp[88]) {
            y -= 20000;
        }
    }

    var enemu;
    //var cs
    with (obj_star) {
        var cs = instance_nearest(x, y, obj_crusade);

        if (point_distance(x, y, cs.x, cs.y) > cs.radius) {
            y -= 20000;
        }
        enemu = 0;

        var nids = array_reduce(
            p_tyranids,
            function(prev, curr) {
                return prev || curr > 3;
            },
            false
        );

        var tau = array_reduce(
            p_tau,
            function(prev, curr) {
                return prev || curr > 0;
            },
            false
        );

        enemu += nids + tau;

        if (present_fleet[eFACTION.ELDAR] > 0) {
            enemu += 2;
        }
        if (present_fleet[eFACTION.ORK] > 0) {
            enemu += 2;
        }
        if (present_fleet[eFACTION.TAU] > 0) {
            enemu += 2;
        }
        if (present_fleet[eFACTION.TYRANIDS] > 0) {
            enemu += 2;
        }
        if (present_fleet[eFACTION.CHAOS] > 0) {
            enemu += 2;
        }
        //nothing for heritics faction
        if (present_fleet[eFACTION.NECRONS] > 0) {
            enemu += 2;
        }
    }
    var ns = instance_nearest(x, y, obj_star);
    var ok = false;
    var max_dist = 800;
    var min_dist = 40;
    var to_ignore = [
        eFACTION.IMPERIUM,
        eFACTION.MECHANICUS,
        eFACTION.INQUISITION,
        eFACTION.ECCLESIARCHY
    ];

    var dist = point_distance(x, y, ns.x, ns.y);
    var valid_target = !array_contains_ext(ns.p_owner, to_ignore, false);
    if (valid_target && dist <= max_dist && dist >= min_dist && (owner == eFACTION.IMPERIUM)) {
        ok = true;
    }

    // if ((ns.owner>5) or (ns.owner  = eFACTION.PLAYER)) and (point_distance(x,y,ns.x,ns.y)<=max_dis) and (point_distance(x,y,ns.x,ns.y)>40) and (owner = eFACTION.IMPERIUM){
    if (ok) {
        action_x = ns.x;
        action_y = ns.y;
        set_fleet_movement();
        orbiting.present_fleet[owner] -= 1;
        home_x = orbiting.x;
        home_y = orbiting.y;

        var i;
        i = 0;
        repeat (orbiting.planets) {
            i += 1;
            if ((orbiting.p_owner[i] == eFACTION.IMPERIUM) && (orbiting.p_guardsmen[i] > 500)) {
                guardsmen += round(orbiting.p_guardsmen[i] / 2);
                orbiting.p_guardsmen[i] = round(orbiting.p_guardsmen[i] / 2);
            }
        }

        alarm[5] = 2;

        with (obj_crusade) {
            if (y < -10000) {
                y += 20000;
            }
        }
        with (obj_crusade) {
            if (y < -10000) {
                y += 20000;
            }
        }
        with (obj_star) {
            if (y < -10000) {
                y += 20000;
            }
        }
        with (obj_star) {
            if (y < -10000) {
                y += 20000;
            }
        }

        exit;
    }

    with (obj_crusade) {
        if (y < -10000) {
            y += 20000;
        }
    }
    with (obj_crusade) {
        if (y < -10000) {
            y += 20000;
        }
    }
    with (obj_star) {
        if (y < -10000) {
            y += 20000;
        }
    }
    with (obj_star) {
        if (y < -10000) {
            y += 20000;
        }
    }
}
